\begin{introduction}
    \item 表达式
    \item 运算符
    \item 优先级
    \item 结合性
    \item 运算符字面量
    \item 表达式修饰符
    \item 常量表达式
    \item 惰性表达式
    \item 表达式异常
\end{introduction}

\section{表达式语法}

\textbf{表达式（Expression）}是 AutoScript 中所有能被\emph{求值}的结构，以变量、字面量、运算符、函数调用相互嵌套组成。所有的运算符都可以分类为下面的几种形式：

\begin{grammar}[表达式] \label{grm:expression}
\begin{enumerate}
	\item \texttt{[atom-expr]}
	\item \texttt{[compound-expr]}
	\item \texttt{[expr-1] [expr-2]}
	\item \texttt{[expr-1] [op] [expr-2]}
\end{enumerate}
\end{grammar}

它们对应的含义如下：

\begin{enumerate}
	\item 对应了一个变量或字面量。
	\item 对应了一个复合表达式。
	\item 对应了一个函数调用表达式，其中 \texttt{[expr-1]} 是可调用的表达式，而 \texttt{[expr-2]} 是\emph{参数}。
	\item 对应了一个运算符调用表达式，其中 \texttt{[expr-1]} 是\emph{左侧表达式}，\texttt{[expr-2]} 是\emph{右侧表达式}。
\end{enumerate}

\subsection{原子表达式}

原子表达式是 AutoScript 中最小的结构。它的类型就是变量或字面量的类型，值就是变量或字面量的值。

\begin{lstlisting}
const main = func () -> do {
    auto x = 42;                // 42 是一个原子表达式
    auto y = x;                 // x 是一个原子表达式
};
\end{lstlisting}

\subsection{do 表达式和 monad 表达式}

AutoScript 中的复合表达式中有两种尤其重要，它们是 \lstinline!do! 表达式和 \lstinline!monad! 表达式。

\begin{grammar}[\texttt{do} 表达式] \label{grm:do-expression}
	\lstinline!do [block]!
\end{grammar}

\begin{grammar}[\texttt{monad} 表达式] \label{grm:monad-expression}
	\lstinline!monad! \texttt{[class-id] [block]}
\end{grammar}

这些表达式的值与类型由其中的 \lstinline!return! 或 \lstinline!yield! 等语句决定。

\begin{lstlisting}
const main = funct () -> do {
    auto z = do {
        scan x : Int;
        return Math.max(x, 0);
    };                          // do { ... } 是一个 do 表达式
    auto w = monad Option {
        return 42;
    };                          // monad MonadClass { ... } 是一个 monad 表达式
};
\end{lstlisting}

其中 \lstinline!do! 表达式和 \lstinline!monad! 表达式默认要求所有 \lstinline!return! 语句后的类型完全相同 \footnote{如果没有提前声明返回类型，则会将第一个 \lstinline!return! 或 \lstinline!yield! 语句作为推断依据。随后出现的这些返回值需要能够隐式转换为推断出来的类型。}。不过，如果想要让 \lstinline!do! 表达式同时接纳所有可能的返回类型（即使用选择类型），则需要在 \lstinline!do! 的表达式中使用 \lstinline!choice! 属性 \footnote{如果编译器发现所有 \lstinline!return! 语句后的表达式类型相同，则会推断返回类型为一个普通的类型。不要滥用 \lstinline!choice! 的便利性，它可能会让函数类型推断变得更加复杂。}：

\begin{lstlisting}
const f = funct (x : Int) -> do choice {
    if (x < 0) {
        return 42;
    }
    if (x < 100) {
        return "abc";
    }
    return True;
};
const main = funct () -> do {
    print typeof(f(0));     // 输出 Int | Prelude.Bool | String
};
\end{lstlisting}

我们会尽量避免使用 \lstinline!choice! 属性。 \\

另一边，\lstinline!monad! 表达式会对 \lstinline!return! 语句的返回值进行一个包装。以 \lstinline!Prelude.Option! 为例，在 \lstinline!monad Option! 表达式中，所有的返回值都会尝试构造成 \lstinline!Option!。

\begin{lstlisting}
const main = funct () -> monad Option {
	scan x : Int;
	if (x > 0) {
		return x;
	}
	return Null;
};
\end{lstlisting}

上面的 \lstinline!Option! 是一个范型抽象数据类型，我们可以通过特定类型的实体或 \lstinline!Null! 来构建。我们会在单子的章节详细介绍这个表达式。

\subsection{调用表达式}

AutoScript 中的调用表达式指的是函数调用，其中特别定义了一些关键字用于函数调用，如我们已经见过的 \lstinline!sizeof!、\lstinline!typeof! 等。运算符的调用也算在调用表达式中，因此内置的 \lstinline!as! 表达式也属于调用表达式。此后我们会遇到更多这样的关键字。\\

调用表达式是右结合的，因此 \lstinline!f g x! 会被理解为 \lstinline!f (g x)! 而非 \lstinline!(f g) x!，这也是为了让内置的这些函数调用起来更加简便（比如可以用 \lstinline!sizeof typeof(Int)! 而非 \lstinline!sizeof(typeof(Int))! \footnote{这和 Haskell 的设定不同，需要注意。}。 \\

在调用表达式中出现的括号并不是其必须的成分，其作用只在于表达式的分组，和其它类型表达式中出现的括号一致；只不过，在一些情况下我们不得不使用调用表达式来让程序行为和预期一致：比如 \lstinline!f(1, 2)! 用于调用一个接受一个两元组的函数 \lstinline!f!，但表达式 \lstinline!f 1, 2! 则会尝试调用 \lstinline!f 1!，随后和 \lstinline!2! 形成一个两元组，含义迥然不同。

\subsection{match 表达式}

AutoScript 中可以用 \lstinline!match! 关键字来引入一个模式匹配表达式。不过目前我们还没有足够的知识来介绍这一类表达式，因此暂用一些简单的例子来体验 \lstinline!match! 表达式。

\begin{lstlisting}
const main = funct () -> do {
    scan x : Int;
    print match (x)
        ? 0  -> "Zero",			// 如果 x 的值是 0
        ? 42 -> "Good",			// 如果 x 的值是 42
        otherwise -> "Bad";     // otherwise 容纳了所有其它情况
};
\end{lstlisting}

\lstinline!match! 表达式依次检查的返回值是通过下面这些“类似于函数表达式”的右侧类型决定的。它要求每个表达式拥有相同的类型。

\subsection{算术表达式}

算术表达式是包含加法运算符 \lstinline!+!、乘法运算符 \lstinline!*!、减法运算符 \lstinline!-! 或除法运算符 \lstinline!/! 的运算符表达式。它拥有下面的\emph{预设} \footnote{所谓预设就是语言和标准库保证满足，且建议程序员也遵守的规则。不过，即使没有遵守也不会造成编译错误，但可能会造成运行时功能偏离预期。这是面向\emph{契约}编程的特点。}：

\begin{itemize}
    \item 加法运算符 \lstinline!+! 一般要求满足交换律和结合律，且不会抛出异常。

    \item 乘法运算符 \lstinline!*! 一般要求满足交换律和结合律，且不会抛出异常。

    \item 减法运算符 \lstinline!-! 一般要求不会抛出异常。

    \item 除法运算符 \lstinline!/! 没有任何要求。
\end{itemize}

内置支持算术运算符的类型包括整数和实数，还有 \lstinline!Prelude! 中定义的一系列算术类型。

\begin{lstlisting}
const main = funct () -> do {
    auto x = 42 / 24;       // Int 之间的是整数除法，因此结果是 1
    auto y = 1.0 * 2.0;     // 结果是 2.0
};
\end{lstlisting}

\subsection{位运算表达式}

位运算表达式是包含按位与运算符 \lstinline!bitand!、按位或运算符 \lstinline!bitor!、左移运算符 \lstinline!shl!、右移运算符 \lstinline!shr! 或按位异或运算符 \lstinline!xor! 的运算符表达式，它们没有任何预设。

\begin{lstlisting}
const main = funct () -> do {
    print 1 bitand 0;       // 输出 0
    print 1 xor 0;          // 输出 1
};
\end{lstlisting}

\subsection{比较表达式}

比较表达式是包含小于运算符 \lstinline!<!、小于等于运算符 \lstinline!<=!、大于运算符 \lstinline!>!、大于等于运算符 \lstinline!>=!、等于运算符 \lstinline!==!、不等于运算符 \lstinline!<>! 或三路比较运算符 \lstinline!<=>! 的运算符表达式。它们拥有下面的预设：

\begin{itemize}
    \item 除了三路比较运算符 \lstinline!<=>! 外所有运算符都返回 \lstinline!Prelude.Bool! 类型。

    \item 三路比较运算符 \lstinline!<=>! 应该返回一个可以和 \lstinline!0! 比较的对象，且其等于零当且仅当 \lstinline!==! 返回真、大于零当且仅当 \lstinline!>! 返回真、小于零当且仅当 \lstinline!<! 返回真 \footnote{显然，AutoScript 在三路比较运算符上相当程度地借鉴了 C++ 的设计。只不过 AutoScript 并没有强有序、弱有序和部分有序的分类。}。
    
    \item 小于运算符 \lstinline!<!、大于运算符 \lstinline!>! 一般要求满足反对称性，且不会抛出异常。

    \item 小于等于运算符 \lstinline!<=! 返回值为真当且仅当 \lstinline!<! 或 \lstinline!==! 返回真。大于等于运算符同理。

    \item 不等于运算符 \lstinline!<>! 返回值为真当且仅当 \lstinline!==! 返回假。
\end{itemize}

\begin{lstlisting}
const main = funct () -> do {
    print 1 < 2;            // 输出 True
    print 1 <=> 2;          // 输出 -1
};
\end{lstlisting}

\subsection{逻辑表达式与条件表达式}

逻辑表达式是包含与运算符 \lstinline!and! 或或运算符 \lstinline!or! 的表达式。它们拥有下面的预设：

\begin{itemize}
	\item \lstinline!and! 运算符的两个参数和返回类型都应该能隐式转换为 \lstinline!Prelude.Bool! 类型。

    \item 两个运算符都应该拥有\emph{短路}性质。对 \lstinline!and! 来说，如果第一个参数可以转换为 \lstinline!False!，则表达式直接得到一个结果；对 \lstinline!or! 来说，如果第一个参数不是 \lstinline!undefined!，则表达式直接得到一个结果。短路发生时第二个参数不会被求值。
\end{itemize}

\begin{lstlisting}
const main = funct () -> do {
	auto danger = funct () -> do { print undefined; return False; };
    print False and danger();		// 输出 False，因为 danger() 并没有被求值
    print True or danger();			// 输出 True，因为 danger() 并没有被求值
};
\end{lstlisting}

特别地，如果一个类型上没有定义转换到 \lstinline!Prelude.Bool! 的函数，则会默认判断它是否为 \lstinline!undefined!，且仅在此时返回一个 \lstinline!False!，否则返回 \lstinline!True!。

\begin{lstlisting}
const main = funct () -> do {
	print "Hello," or " world!";	// 输出 Hello,
	print undefined and 42;			// 运行期错误，对 undefined 求值
};
\end{lstlisting}

这个性质加上逻辑表达式短路的特性，可以用来实现条件表达式。

\begin{grammar}[条件表达式 \texttt{[cond-expr]}] \label{grm:conditional-expression}
    \texttt{[expr-cond]} \lstinline!and! \texttt{[expr-1]} \lstinline!or! \texttt{[expr-2]}
\end{grammar}

上面的表达式中，首先会对 \lstinline![bool-expr] and [expr-1]! 进行求值，若 \lstinline![bool-expr]! 为真，则会返回 \lstinline![expr-1]! 的值，否则返回 \lstinline![expr-2]! 的值。这个特性的一个致命缺陷在于当 \lstinline![expr-1]! 本身可以显式转换为 \lstinline!False! 时，条件表达式依然会返回 \lstinline![expr-2]!。在内置类型中，这样的表达式只有 \lstinline!False! 和 \lstinline!undefined! \footnote{如果你有 C++ 等语言的背景，这里要特别注意：AutoScript 中的 \lstinline!Bool! 不是内置类型， \lstinline!0! 并不能被强制类型转换为 \lstinline!False!。}，因此这并不是一个显著的问题。

\begin{lstlisting}
const main = funct () -> do {
    scan x : Int;
    print x < 10 and "Small"    // 若 x 小于 10 则输出 Small
       or x > 20 and "Big"      // 若 x 大于 20 则输出 Big
       or "Good";               // 其余情形输出 Good
};
\end{lstlisting}

条件表达式中 \lstinline!and! 右侧的类型应该完全一致，否则编译器就无法成功推导表达式的类型。但我们可以使用显式类型转换。

\begin{lstlisting}
const main = funct () -> do {
	print (x < 10 and "Good"
		or x > 20 and undefined
		or 42) as Bool;			// 声明为 Bool 类型的表达式
};
\end{lstlisting}

\subsection{类型转换表达式}

类型转换表达式是包含类型转换运算符 \lstinline!as! 或 \lstinline!of! 的表达式。前者的行为总是固定的（即在元组类型和相同内存布局的其它复合类型之间的转换），但后者允许通过属性 \lstinline!@ctor! 来覆写其行为，我们会在结构章节中详细说明。

\begin{lstlisting}
const Point = struct (x : Real, y : Real);

const main = funct () -> do {
    auto p1 = (1.0, 0.0) as Point;
    auto p2 = Point of (1.0, 0.0);
};
\end{lstlisting}


\subsection{其它运算符表达式}

AutoScript 提供了 \lstinline!in! 运算符来判断某个元素在一个范围中是否出现。

\begin{lstlisting}
const main = funct () -> do {
    auto arr = (1, 2, 3);
    print 1 in arr;         // 输出 True
    print 0 in arr;         // 输出 False
};
\end{lstlisting}

在 \lstinline!Prelude! 中定义了字符串的拼接运算符 \lstinline!++!。

\begin{lstlisting}
const main = funct () -> do {
    auto str = "abc";
    print str ++ "def";     // 输出 abcdef
};
\end{lstlisting}

下一节中我们将介绍如何自定义运算符。


\subsection{运算符}

运算符的优先级有高低之分，这是为了照顾一些管用的表达式形式。比如 \lstinline!1 + 2 * 3! 中为了迎合数学的习惯，会优先执行 \lstinline!2 * 3! 得到 \lstinline!6!，随后执行 \lstinline!1 + 6!。此外，对于同样优先级的运算符，它们不通过圆括号分组时，会按照特定的顺序运算（从左到右或从右到左），这被称为\emph{左结合}和\emph{右结合}。下面用一张表展示所有 AutoScript 中运算符的优先级和结合性。

\begin{table}[h]
    \centering
    \begin{tabular}{|c|c|c|} \hline
        运算符 & 优先级 & 结合性 \\\hline
        \lstinline!,! & 0 & 左 \\\hline
        \lstinline!->! & 1 & 右 \\\hline
        \lstinline!<-! & 2 & 右 \\\hline
        逻辑运算符 & 3 & 左 \\\hline
        比较运算符（\lstinline!<=>! 以外） & 4 & 左 \\\hline
        \lstinline!<=>! & 5 & 左 \\\hline
        位运算符 & 6 & 左 \\\hline
        \lstinline!+!、\lstinline!-! & 7 & 左 \\\hline
        \lstinline!*!、\lstinline!/! & 8 & 左 \\\hline
        \lstinline!^! & 9 & 左 \\\hline
        \lstinline!as!、\lstinline!of!、\lstinline!hasbase!、\lstinline!hasclass!、\lstinline!in! & 10 & 左 \\\hline
        调用运算符 & 11 & 右 \\\hline
    \end{tabular}
    \caption{运算符的优先级和结合性}
    \label{tab:operators}
\end{table}

AutoScript 支持自定义运算符，我们可以通过 \lstinline!@operator! 属性来给出这个运算符的优先级和结合性。默认的情况下是最高优先级和左结合性。

\begin{lstlisting}
@operator(9, left)
const <+> = funct (x : Int) -> funct (y : Int) -> x^2 + y^2;  // 这个运算符和 + 一样的性质

const main = funct () -> do {
    print 1 + 2 <+> 3;      // 输出 18
};
\end{lstlisting}

虽然运算符直接支持中缀形式，但我们总是可以将其当作普通函数使用。不过由于 AutoScript 对符号组成的名字特殊对待（默认将其视作中缀形式的运算符），因此为了让它能够作为前缀使用，需要在外面套上一个括号。类似地，由字母组成的名字如果要以中缀形式使用，需要写成\emph{运算符字面量}的形式，即将其用反引号包围。

\begin{lstlisting}
@operator(9, left)
const add = funct (x : Int) -> funct (y : Int) -> x + y;

const main = funct () -> do {
    print (+)(1)(2);        // 输出 3
    print 1 `add` 2;        // 输出 3
};
\end{lstlisting}

\subsection{自定义字面量}

AutoScript 中可以自定义字面量，其本质是一个函数调用。字面量函数要求函数接收一个数字或字符串（内置的类型），并在声明处使用 \lstinline!@literal! 属性。这个属性修饰的函数能且仅能接受一个参数。

\begin{lstlisting}
@literal
const dozen = funct (n : Int) -> 12 * n;

const main = funct () -> do {
	print 2dozen;			// 输出 24
};
\end{lstlisting}

当然，字面量函数可以当作函数使用：

\begin{lstlisting}
@literal
const k = funct (n : Int) -> 1000 * n;

const main = funct () -> do {
	print k(1);				// 输出 1000
	print 1k;				// 输出 1000
};
\end{lstlisting}

在使用自定义字面量时，可能会出现隐式类型转换。

\begin{lstlisting}
@literal
const dozen = funct (n : Real) -> 12 * n;

const main = funct () -> do {
	print 1dozen;			// 输出 12.0
};
\end{lstlisting}

\section{表达式修饰符}

AutoScript 中允许在表达式的最外面添加修饰符，用来表示其求值性质或求值得到的对象性质。下表列出了所有的表达式修饰符：

\begin{table}[!ht]
    \centering
    \begin{tabular}{|c|c|c|} \hline
        关键字 & 简介 & 类型 \\\hline
        \lstinline!atom!        & 原子修饰符         & 1 \\\hline
        \lstinline!await!		& 异步表达式修饰符		& 3 \\\hline
        \lstinline!class!       & 类族提示符         & 2 \\\hline
        \lstinline!comptime!    & 常量表达式修饰符    & 3 \\\hline
        \lstinline!dyn!         & 动态修饰符         & 1 \\\hline
        \lstinline!fixed!		& 恒值表达式修饰符		& 3 \\\hline
        \lstinline!funct!        & 函数提示符         & 2 \\\hline
        \lstinline!import!		& 导入修饰符			& 3 \\\hline
        \lstinline!lazy!        & 惰性表达式修饰符    & 3 \\\hline
        \lstinline!mut!         & 可变修饰符         & 1 \\\hline
        \lstinline!object!      & 具名元组提示符      & 2 \\\hline
        \lstinline!ref!         & 引用修饰符         & 1 \\\hline
        \lstinline!struct!      & 结构提示符         & 2 \\\hline
    \end{tabular}
    \caption{表达式修饰符}
    \label{tab:expression-qualifier}
\end{table}

可以看到，我们可以将所有表达式修饰符分为三个类型：

\begin{enumerate}
    \item 修饰表达式求值后结果的修饰符，也称为\textbf{实体修饰符（Entity Qualifier）}。

    \item 提示表达式求值后结果范畴的修饰符，也称为\textbf{范畴提示符（Category Indicator）}。

    \item 提示表达式求值方式的修饰符，也称为\textbf{求值修饰符（Evaluation Qualifier）}
\end{enumerate}

下面让我们逐个介绍这些修饰符的作用。

\subsection{实体修饰符}

实体修饰符包含了 \lstinline!atom!、\lstinline!dyn!、\lstinline!mut! 和 \lstinline!ref!。其特点在于其修饰表达式后会让表达式求值的类型加上这些修饰符。比如 \lstinline!mut 42! 的求值类型是 \lstinline!mut Int!。此外，除了 \lstinline!atom! 和 \lstinline!mut! 的相互顺序不影响表达式类型，如 \lstinline!atom mut 42! 和 \lstinline!mut atom 42! 没有区别。

\begin{lstlisting}
const main = funct () -> do {
    auto x = atom dyn mut 42;   // 类型为 atom dyn mut Int
};
\end{lstlisting}

如果要同时使用 \lstinline!ref! 和其它修饰符，则需要注意顺序：\lstinline!ref! 本身可以被修饰，因此在其左侧出现的修饰符修饰的是引用类型，右侧出现的则修饰被引用类型。

\begin{lstlisting}
const main = funct () -> do {
    auto x = atom ref dyn ref mut ref 42;   // 类型为 atom ref dyn ref mut ref Int
};
\end{lstlisting}

\lstinline!dyn! 和 \lstinline!ref! 的性质类似，我们需要注意其它修饰符和 \lstinline!dyn! 的相对顺序。下面是对这些修饰符的展开介绍：

\begin{itemize}
	\item \lstinline!atom! 声明了一个原子类型，对这个类型对象的读写都是原子操作，保证线程安全。
	
	\item \lstinline!dyn! 声明了一个动态类型，对这个类型上可以进行运行期的动态删改。
	
	\item \lstinline!mut! 声明了一个可变类型，我们此前已经介绍过了。
	
	\item \lstinline!ref! 声明了一个引用类型，我们此前已经介绍过了。
\end{itemize}

我们会在高级特性部分中深入讲解 \lstinline!atom! 和 \lstinline!dyn! 的用法。

\subsection{范畴修饰符}

类别修饰符包括了 \lstinline!class!、\lstinline!funct!、\lstinline!object! 和 \lstinline!struct!。它们分别对应了 AutoScript 中的类族、函数、对象和结构，常用于声明变量时的初始化表达式。它们存在的意义是为了让编译器能提前确定表达式的含义，从而简化编译过程。这其中 \lstinline!class! 和 \lstinline!object! 关键字我们还没有遇到过，详细内容请见后文。 \\

这里值得一提的是，范畴修饰符可以在合适的时机被省略。以函数为例，如果我们提前声明了函数的类型，那么就可以在定义时省略 \lstinline!funct! 关键字。以完整的变量定义为例：

\begin{lstlisting}
const f : Int -> Int = x -> x;		// 这里不仅省略了 funct 关键字，还省略了参数外侧的括号
\end{lstlisting}

结构声明中，\lstinline!struct! 也可以在一定条件下被省略，我们会在结构的章节中介绍。

\subsection{await 表达式}

AutoScript 中用 \lstinline!await! 表达式来并发执行某个表达式。

\begin{lstlisting}
import Time;

const sleep = funct (sec : Int) -> do {
    print "Ready to sleep for $(sec) seconds";
    await suspend_for(Time.from_second(sec));
    print "Wait done";
};

const main = funct () -> do {
    sleep(1);
};
\end{lstlisting}

\lstinline!await! 表达式的机制比较复杂，因此这里不详细介绍了。我们会在并发的章节中详细介绍这个机制。

\subsection{常量表达式}

AutoScript 中存在常量存储期来表示编译期的常数。除此之外，我们也可以用 \lstinline!comptime! 关键字来引入一个常量表达式。这会促使编译器在编译期对 \texttt{[expr]} 进行检查并求值。

\begin{lstlisting}
const main = funct () -> do {
    auto ans = comptime do {
        auto x = 42;
        auto y = 24;
        if (x > y) {
            return "abc";
        }
        return "def";
    };
};
\end{lstlisting}

因此，我们可以轻易地定义编译期函数，即强制在编译期进行所有函数调用的函数。

\begin{lstlisting}
const const_add = funct (x : Int, y : Int) -> comptime (x + y);

const const_sum = funct (x : Int, y : Int) -> comptime do {
    auto res = const_add(x, y);
    print res;
};
\end{lstlisting}

值得说明的是，\lstinline!comptime! 并不要求其后表达式中出现的函数被声明为 \lstinline!comptime!，但如果在编译时发现无法把值在编译期算出，会报编译错误。

\begin{lstlisting}
const const_error = funct () -> comptime do {
    scan x : Int;       // 编译错误，scan 语句无法做到编译期
    print x;
};
\end{lstlisting}

此外，\lstinline!comptime! 让编译器强制将某个表达式进行编译期求值，但编译器有权力将其它表达式也放在编译期求值。只要这是可能的。

\begin{lstlisting}
const f = funct () -> 42;		// 并没有标记为 comptime

const main = funct () -> do {
	auto x = comptime f();		// 没问题，因为 f() 可以在编译期求值
};
\end{lstlisting}

这是因为“能在编译期求值”、“强制在编译期求值”和“确实在编译器求值”是三个不同的事件（后两者有较强的相关性）\footnote{对 C++ 熟悉的读者可以将“能在编译器求值”对应 \cpp{constexpr} 函数，“强制在编译期求值”是 \cpp{constexpr} 变量或 \cpp{consteval} 函数，“确实在编译器求值”则是所有常量表达式，比如枚举量、非类型模版参数、\cpp{constexpr} 变量等。}。一般来讲我们不需要使用 \lstinline!comptime! 关键字；后面介绍模版元编程的章节中我们会看到它的用处。

\subsection{fixed 表达式}

在 \lstinline!monad! 表达式中，所有变量定义、赋值运算和返回表达式都会被包装为指定的单子类型；如果想要保持某个表达式的类型不被修改，可以使用 \lstinline!fixed! 关键字。

\begin{lstlisting}
const main = funct () -> monad Option {
	auto x = 42;
	return fixed x;		// 返回 42 而不是 Option[Int] 类型
};
\end{lstlisting}

详见单子章节。

\subsection{惰性表达式}

惰性表达式是通过 \lstinline!lazy! 关键字构建的，它会推迟某个表达式的求值。

\begin{lstlisting}
const main = funct () -> do {
    auto x = lazy 42;       // 不对 x 进行求值
    auto y = lazy x + 20;   // 不对 y 进行求值
    print y;                // 对 x 和 y 都进行求值，输出 62
};
\end{lstlisting}

由于 \lstinline!lazy! 并没有传染性，在任何常规的求值环境中都会迫使其中的惰性表达式被求值，因此只有反复使用 \lstinline!lazy! 关键字才能让一个惰性表达式持续延后求值。 \\

注意，\lstinline!lazy! 并不是类型的一部分（因为实际上它没有影响对象的性质）。

\subsection{导入修饰符}

导入修饰符是和模块系统相关的关键字。不过它的一些用法不限于狭义的模块。我们可以在 \lstinline!import! 后的表达式中使用不在当前名字空间中定义的标识符。

\begin{lstlisting}
// main.asc
const main = funct () -> do {
	auto p : import Utility.Pair = (1, 0);	// 导入了 Utility 模块中的 Pair 类型
};
\end{lstlisting}


\section{表达式异常}

本节虽然不打算详细介绍\emph{异常}，但有必要提及表达式的\emph{异常类型}。一个表达式除了拥有类型以外，还有一个可选的异常类型。异常可以通过 \lstinline!throw! 语句产生。

\begin{lstlisting}
const main = funct () -> do {
    auto x = do {
    	scan b : Bool;
    	if (b) {
    	    throw 42;
    	}
    };              // x 的表达式类型为 Void，但异常类型是 Int，因为它可能抛出 Int 对象
    auto y = do {
    	return 42;
    };				// y 的表达式类型为 Int，而异常类型是 ()。这也被称为无异常表达式
};
\end{lstlisting}

\lstinline!throw! 语句抛出的异常会通过一层层作用域传递，除非在某个作用域中被捕获。AutoScript 中表达式的异常类型可以通过 \lstinline!exceptof! 函数获得。

\begin{lstlisting}
const main = funct () -> do {
    auto x = lazy do { throw 42 };			// 惰性求值，否则会立刻抛出异常
    auto y = funct () -> do { throw 42; };
    print exceptof(x);                      // 输出 Int
    print exceptof(y());                    // 输出 Int
};
\end{lstlisting}

除了 \lstinline!throw! 语句可以产生异常外，任何 \lstinline!monad! 表达式都拥有非空的异常类型。

\begin{lstlisting}
const main = funct () -> do {
	auto x = monad Option { return 42; };
	auto y = monad Expected[Or = String] { return 42; };
	print exceptof(x);						// 输出 Null
	print exceptof(y);						// 输出 String
};
\end{lstlisting}

我们将在单子章节中详细介绍两者的联系之处。